/*
Кузнецов Даниил группа M60-209б
Вариант 316
В небольшом районном городе живут пятеро друзей: Иванов, Петренко, Сидорчук, Гришин, Капустин.
Профессии у них разные: один из них - маляр, другой — мельник, третий — плотник, четвертый — почтальон, пятый - парикмахер.
Петренко и Гришин никогда не держали в руках малярной кисти.
Иванов и Гришин все собираются посетить мельницу, где работает их товарищ.
Петренко и Капустин живут в одном доме с почтальоном.
Сидорчук был недавно одним из свидетелей на свадьбе Петренко и дочери парикмахера.
Иванов и Петренко каждое воскресенье играют в городки с плотником и маляром.
Гришин и Капустин по субботам обязательно встречаются в парикмахерской, где работает их друг.
Почтальон предпочитает бриться сам.
Определите профессию каждого из друзей.
*/

CONSTANTS
	все_друзья                                     = [иванов, петренко, сидорчук, гришин, капустин]
DOMAINS
	друг                                           = человек(фамилия,профессия)
	список_друзей                                  = друг*

	фамилия                                        = иванов; петренко; сидорчук; гришин; капустин
	иванов, петренко, сидорчук, гришин, капустин   = symbol
	список_фамилий                                 = фамилия*

	профессия                                      = маляр; мельник; плотник; почтальон; парикмахер
	маляр, мельник, плотник, почтальон, парикмахер = symbol
	список_профессий                               = профессия*
PREDICATES
	nondeterm входит_в           (профессия, список_профессий)
	nondeterm входит_в           (фамилия, список_фамилий)
	nondeterm входит_в           (друг, список_друзей)
	nondeterm без_повторов       (список_фамилий)
	nondeterm совместный_досуг   (список_друзей, список_фамилий, профессия)
	nondeterm генерация_гипотезы (список_друзей)
CLAUSES
%% Проверка, входит ли друг в список друзей
	входит_в(Друг, [ Друг | _ ]).
	входит_в(Друг, [ _ | Хвост_Списка_Друзей ])
	:-
		входит_в(Друг, Хвост_Списка_Друзей)
	.

	без_повторов([_]).
	без_повторов([ Первый | Хвост_Списка_Друзей ])
	:-
		без_повторов(Хвост_Списка_Друзей),
		not(входит_в(Первый, Хвост_Списка_Друзей))
	.
%% Совместный досуг
	совместный_досуг(Список_друзей, Имена_известных, Профессия)
	:- 
		входит_в(человек(Имя, Профессия), Список_друзей),
    	not( входит_в(Имя, Имена_известных) )
	.
%% Генерация гипотезы
	%% Гипотеза моей задачи: предположение о возможных свойствах друзей
	генерация_гипотезы(Список_друзей)
	:-
		входит_в(Друг1, все_друзья),
		входит_в(Друг2, все_друзья),
		входит_в(Друг3, все_друзья),
		входит_в(Друг4, все_друзья),
		входит_в(Друг5, все_друзья),
		%% Условие (без повторений)
		Список_фамилий = [Друг1, Друг2, Друг3, Друг4, Друг5],
		без_повторов(Список_фамилий),
		Список_друзей = 
		[
			человек(Друг1, парикмахер),
			человек(Друг2, мельник),
            человек(Друг3, почтальон),
            человек(Друг4, плотник),
            человек(Друг5, маляр)
		],
		%% Проверка условий
		%% 1. Петренко и Гришин никогда не держали в руках малярной кисти
		входит_в(человек(Маляр, маляр), Список_друзей),
		not(Маляр = гришин),
		not(Маляр = петренко),
		%% 2. Иванов и Гришин все собираются посетить мельницу, где работает их товарищ.
		совместный_досуг(Список_друзей, [иванов, гришин],          мельник),
		%% 3. Петренко и Капустин живут в одном доме с почтальоном.
		совместный_досуг(Список_друзей, [петренко, капустин],      почтальон),
		%% 4. Сидорчук был недавно одним из свидетелей на свадьбе Петренко и дочери парикмахера.
		совместный_досуг(Список_друзей, [сидорчук, петренко],      парикмахер),
		%% 5. Иванов и Петренко каждое воскресенье играют в городки с плотником и маляром.
		совместный_досуг(Список_друзей, [иванов, петренко],        плотник),
		совместный_досуг(Список_друзей, [иванов, петренко],        маляр),
		%% 6. Гришин и Капустин по субботам обязательно встречаются в парикмахерской, где работает их друг.
		Посетители_парикмахерской = [гришин, капустин],
		совместный_досуг(Список_друзей, Посетители_парикмахерской, парикмахер),
		%% 7. Почтальон предпочитает бриться сам.
		входит_в(человек(Имя, почтальон), Список_друзей),
		not( входит_в(Имя, Посетители_парикмахерской) )
	.
GOAL
	генерация_гипотезы(Список_друзей).

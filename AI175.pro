%  Выполнила: Сёмина М.В. 6О-208Б-17. Задача №AI175. 
%
%                           Разбитое окно
% Во время перемены в классе оставались Ангелика, Бернд, Вольфганг и Мануэла.
% Кто-то из них разбил стекло. Учитель опросил ребят и получил от каждого по три ответа.
% Ангелика:     1. Окно разбила не я.
%               2. Я сидела в классе и читала.
%               3. Мануэла знает, кто разбил окно
%
% Бернд:        1. Это сделал не я.
%               2. С Мануэлой я уже давно не разговариваю.
%               3. Окно разбил Вольфганг.
%
% Вольфганг:    1. Я не виноват.
%               2. Окно разбила Мануэла.
%    3. Бернд лжет, когда утверждает, будто окно разбил я.
%
% Мануэла:  1. Окно разбила не я.
%     2. Ангелика разбила окно.
%     3. Бернд знает, что я не виновата, ведь на перемене мы с ним играли вместе.
% Кроме того, каждый из них признался, что из трех ответов два истинных и один ложный.
% Кто разбил окно?

DOMAINS 
    имя                = ангелика; бернд; вольфганг; мануэла
    вина               = разбил; не_разбил
    ученик             = ученик(имя,вина)
    структура_гипотезы = ученик(имя, вина); разговаривают(имя, имя, разговор)
    подозреваемые      = структура_гипотезы*
    номер_высказывания = integer
    разговор           = да; нет
    номера             = integer*
    имена              = имя*
    
PREDICATES
    nondeterm один_из             (структура_гипотезы, подозреваемые)       
    nondeterm количество          (номера, integer) 
    nondeterm количество          (имена, integer) 
    nondeterm количество_истинных (имя, подозреваемые, integer)
    nondeterm возможная_вина      (вина)
    nondeterm возможный_разговор  (разговор)
    nondeterm генерация_гипотезы  (подозреваемые)
    nondeterm проверка_гипотезы   (подозреваемые)
    nondeterm решение_гипотезы    (подозреваемые, имя) -(i,o)
    nondeterm говорит             (имя,номер_высказывания,подозреваемые)
    nondeterm окно_разбил         (имя)
CLAUSES
    один_из(Ученик, [ Ученик | _ ]).
    один_из(Ученик, [ _ | Хвост_Списка_Учеников ])
    :-
        один_из(Ученик, Хвост_Списка_Учеников)
    .
    
    количество([], 0).
    количество([ _Первый | Хвост_количества ], Количество)
    :-
        количество(Хвост_количества, КоличествоОстальных),
        Количество = КоличествоОстальных + 1
    .
    количество_истинных(Имя, Подозреваемые, Количество)
    :-
        findall
        (
            Номер,
            говорит(Имя, Номер, Подозреваемые),
            НомераПравды
        ),
        количество(НомераПравды, Количество)
    .
  
    возможная_вина(разбил).
    возможная_вина(не_разбил).
    возможный_разговор(да).
    возможный_разговор(нет).
        
%% Генерация Гипотезы 
    генерация_гипотезы(Подозреваемые)
    :-
        возможная_вина(ВинаА),
        возможная_вина(ВинаБ),
        возможная_вина(ВинаB),
        возможная_вина(ВинаМ),
        возможный_разговор(Разговаривают),
        Подозреваемые =
        [
            ученик(ангелика, ВинаА), 
            ученик(бернд,ВинаБ), 
            ученик(вольфганг,ВинаB), 
            ученик(мануэла,ВинаМ),
            разговаривают(бернд, мануэла, Разговаривают)
        ],
        findall
        (
            Имя, 
            один_из(ученик(Имя, разбил), Подозреваемые), 
            Виновные
        ),
        количество(Виновные,1)
    .
    проверка_гипотезы(Подозреваемые)
    :-
        КоличествоИстинных = 2,
        количество_истинных(ангелика, Подозреваемые, КоличествоИстинных),
        количество_истинных(бернд,    Подозреваемые, КоличествоИстинных),
        количество_истинных(вольфганг,Подозреваемые, КоличествоИстинных),
        количество_истинных(мануэла,  Подозреваемые, КоличествоИстинных)
    .
    решение_гипотезы(Подозреваемые, Имя)
    :-
        один_из(ученик(Имя, разбил), Подозреваемые)
    .
% Описание высказываний из условий задачи
%
% Ангелика:  1. Окно разбила не я.
%            2. Я сидела в классе и читала = "окно разбила не я"
%            3. Мануэла знает, кто разбил окно.
    говорит(ангелика,1, Подозреваемые)
    :-
        один_из(ученик(ангелика, не_разбил), Подозреваемые)
    .
    говорит(ангелика,2, Подозреваемые)
    :-
        один_из(ученик(ангелика, не_разбил), Подозреваемые)
    .
    говорит(ангелика,3, Подозреваемые)
    :-
        говорит(мануэла,1, Подозреваемые)
    ;
        говорит(мануэла,2, Подозреваемые)
    .
% Бернд:     1. Это сделал не я.
%            2. С Мануэлой я уже давно не разговариваю.
%            3. Окно разбил Вольфганг.
    говорит(бернд,1, Подозреваемые)
    :-
        один_из(ученик(бернд, не_разбил), Подозреваемые)
    .
    говорит(бернд,2, Подозреваемые)
    :-
        один_из(разговаривают(бернд, мануэла, нет), Подозреваемые)
    .
    говорит(бернд,3, Подозреваемые)
    :-
        один_из(ученик(вольфганг, разбил), Подозреваемые)
    .
% Вольфганг: 1. Я не виноват. = "окно разбил не я"
%            2. Окно разбила Мануэла.
%        3. Бернд лжет, когда утверждает, будто окно разбил я.
    говорит(вольфганг,1, Подозреваемые)
    :-
        один_из(ученик(вольфганг, не_разбил), Подозреваемые)
    .
    говорит(вольфганг,2, Подозреваемые)
    :-
        один_из(ученик(мануэла, разбил), Подозреваемые)
    .
    говорит(вольфганг,3, Подозреваемые)
    :-
        not(говорит(бернд, 3, Подозреваемые))
    .
% Мануэла:   1. Окно разбила не я.
%        2. Ангелика разбила окно.
%            3. Бернд знает, что я не виновата, ведь на перемене мы с ним играли вместе.    
    говорит(мануэла,1, Подозреваемые)
    :-
        один_из(ученик(мануэла, не_разбил), Подозреваемые)
    .
    говорит(мануэла,2, Подозреваемые)
    :-
        один_из(ученик(ангелика, разбил), Подозреваемые)
    .
    говорит(мануэла,3, Подозреваемые)
    :-
        один_из(разговаривают(бернд, мануэла, да), Подозреваемые)
    .
        
    окно_разбил(Решение)
    :-
        генерация_гипотезы(Подозреваемые),
        проверка_гипотезы(Подозреваемые),
        решение_гипотезы(Подозреваемые,Решение)
    .
GOAL
    окно_разбил(Окно_разбил).

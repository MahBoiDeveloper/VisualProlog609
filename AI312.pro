/*            
                                            O мужчинах

Статистика, а мы не можем ей не верить, утверждает:
  1) Каждый отец трех сыновей, делающий по утрам зарядку, умеет программировать, даже если он носит очки.
  2) Каждый мужчина в очках имеет трех сыновей или делает по утрам зарядку.
  3) Никакой мужчина, не имеющий трех сыновей, не носит очки, если только он не умеет программировать.
  4) Каждый мужчина, у которого есть три сына и который носит очки, делает по утрам зарядку.
  5) Ни у одного мужчины, умеющего программировать, нет трех сыновей.
Если мы верим статистике то, что можно сказать о мужчинах, которые носят очки?
*/


DOMAINS 
        название, статус = symbol
        свойство = свойство(название, статус)
        мужчина = свойство*
        список_гипотез = мужчина*
/* так как высказываний у нас много, запишем переменную "номер_высказывания" */
        номер_высказывания=integer

PREDICATES
        nondeterm возможный_статус(статус)
        nondeterm входит_в(свойство, мужчина)
        nondeterm входит_в(мужчина, список_гипотез)
        nondeterm знаем_что(номер_высказывания, мужчина)
        nondeterm генерация_гипотезы(мужчина)
        nondeterm список_всех_гипотез(список_гипотез)
        nondeterm отсев_дублей(список_гипотез, список_гипотез)
        nondeterm поиск_решения(название, статус)
CLAUSES 

возможный_статус(да).
возможный_статус(нет).


%   Принадлежность элемента списку
входит_в(Первый,[Первый|_]).
входит_в(Первый,[_|Хвост]):-входит_в(Первый,Хвост).
       
/* 
Высказывание 1. 
Каждый отец трех сыновей, делающий по утрам зарядку, умеет программировать, даже если он носит очки.
*/       

знаем_что(1, Мужчина):- входит_в(свойство(имеет_трех_сыновей, да), Мужчина), входит_в(свойство(делает_зарядку, да), Мужчина),
входит_в(свойство(умеет_программировать, да), Мужчина).
знаем_что(1, Мужчина):- входит_в(свойство(имеет_трех_сыновей, нет), Мужчина); входит_в(свойство(делает_зарядку, нет), Мужчина).

/* 
Высказывание 2. 
Каждый мужчина в очках имеет трех сыновей или делает по утрам зарядку.
*/          

знаем_что(2, Мужчина):- входит_в(свойство(носит_очки, да), Мужчина), входит_в(свойство(имеет_трех_сыновей, да), Мужчина);
                        входит_в(свойство(носит_очки, да), Мужчина), входит_в(свойство(делает_зарядку, да), Мужчина).
знаем_что(2, Мужчина):- входит_в(свойство(носит_очки, нет), Мужчина).

/* 
Высказывание 3. 
Никакой мужчина, не имеющий трех сыновей, не носит очки, если только он не умеет программировать.
*/         

знаем_что(3, Мужчина):- входит_в(свойство(имеет_трех_сыновей, нет), Мужчина), входит_в(свойство(носит_очки, нет), Мужчина),
входит_в(свойство(умеет_программировать, нет), Мужчина).
знаем_что(3, Мужчина):- входит_в(свойство(имеет_трех_сыновей, нет), Мужчина), входит_в(свойство(носит_очки, да), Мужчина),
входит_в(свойство(умеет_программировать, да), Мужчина).
знаем_что(3, Мужчина):- входит_в(свойство(имеет_трех_сыновей, да), Мужчина).

/*
Высказывание 4.
Каждый мужчина, у которого есть три сына и который носит очки, делает по утрам зарядку.
*/

знаем_что(4, Мужчина):- входит_в(свойство(имеет_трех_сыновей, да), Мужчина), входит_в(свойство(носит_очки, да), Мужчина),
входит_в(свойство(делает_зарядку, да), Мужчина).
знаем_что(4, Мужчина):- входит_в(свойство(имеет_трех_сыновей, нет), Мужчина); входит_в(свойство(носит_очки, нет), Мужчина).

/*
Высказывание 5.
Ни у одного мужчины, умеющего программировать, нет трех сыновей.
*/

знаем_что(5, Мужчина):- входит_в(свойство(умеет_программировать, да), Мужчина), входит_в(свойство(имеет_трех_сыновей, нет), Мужчина).
знаем_что(5, Мужчина):- входит_в(свойство(умеет_программировать, нет), Мужчина).


генерация_гипотезы(Мужчина):-
возможный_статус(Статус_1), возможный_статус(Статус_2),
возможный_статус(Статус_3), возможный_статус(Статус_4),

Мужчина = [свойство(имеет_трех_сыновей, Статус_1), свойство(делает_зарядку, Статус_2),
           свойство(умеет_программировать, Статус_3), свойство(носит_очки, Статус_4)],

знаем_что(1, Мужчина),
знаем_что(2, Мужчина),
знаем_что(3, Мужчина),
знаем_что(4, Мужчина),
знаем_что(5, Мужчина).

список_всех_гипотез(Список):- findall(Гипотеза, генерация_гипотезы(Гипотеза), Список).
% отсеивание повторяющихся гипотез
отсев_дублей([Последний], [Последний]).
отсев_дублей([Первый|Хвост_списка], Итоговый_список) :- отсев_дублей(Хвост_списка, Итоговый_список_1),
                                                        not(входит_в(Первый, Итоговый_список_1)),
                                                        Итоговый_список = [Первый|Итоговый_список_1].
отсев_дублей([Первый|Хвост_списка], Итоговый_список) :- отсев_дублей(Хвост_списка, Итоговый_список_1),
                                                        входит_в(Первый, Итоговый_список_1),
                                                        Итоговый_список = Итоговый_список_1.
поиск_решения(Название, Статус):- список_всех_гипотез(Список),
                         отсев_дублей(Список, Список_без_дублей),
                         %Если мы верим статистике то, что можно сказать о мужчинах, которые носят очки?
                         входит_в(Мужчина, Список_без_дублей),
                         входит_в(свойство(носит_очки, да), Мужчина),
                         входит_в(свойство(Название, Статус), Мужчина).
Goal

write("О_мужчине_который_носит_очки_можно_сказать:"),nl,
поиск_решения(Свойство, Статус_свойства).




